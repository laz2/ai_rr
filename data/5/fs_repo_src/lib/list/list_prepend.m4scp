/*
-----------------------------------------------------------------------------
This source file is part of OSTIS (Open Semantic Technology for Intelligent Systems)
For the latest info, see http://www.ostis.net

Copyright (c) 2011 OSTIS

OSTIS is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

OSTIS is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with OSTIS.  If not, see <http://www.gnu.org/licenses/>.
-----------------------------------------------------------------------------
*/

#include "scp_keynodes.scsy"
#include "list_keynodes.scsy"

//
// Создает элемент списка со значение @p v и добавляет его перед элементом @p l.
// Созданный элемент списка возвращает в @p prev. 
// @author Dmitry Lazurkin
//
procedure(list_prepend,
[[
	list_next_;
]],
[{
	arc2, arc4, elt3, old_prev,
	l, v, prev                  // переменные для параметров
}],
{[
	1_:  in_: l,   // элемент списка, перед которым будет добавлен новый элемент
	2_:  in_: v,   // значение для нового элемента списка
	3_: out_: prev // созданный новый элемент списка
]})
	callReturn([
		1_: list_new,
		2_: {[
			1_: v,
			2_: prev
		]}
	])

	searchElStr5([
		1_: assign_: old_prev,
		2_: assign_: arc_: pos_: const_: arc2,
		3_:  fixed_: l,
		4_: assign_: arc_: pos_: const_: arc4,
		5_:  fixed_: list_next_
	],, has_not_prev)

	eraseEl([1_: f_: arc2])

	genElStr5([
		1_:  fixed_: old_prev,
		2_: assign_: arc_: pos_: const_: arc2,
		3_:  fixed_: prev,
		4_: assign_: arc_: pos_: const_: arc4,
		5_:  fixed_: list_next_
	])

label(has_not_prev)
	genElStr5([
		1_:  fixed_: prev,
		2_: assign_: arc_: pos_: const_: arc2,
		3_:  fixed_: l,
		4_: assign_: arc_: pos_: const_: arc4,
		5_:  fixed_: list_next_
	])

	return()
end
