/*
-----------------------------------------------------------------------------
This source file is part of OSTIS (Open Semantic Technology for Intelligent Systems)
For the latest info, see http://www.ostis.net

Copyright (c) 2011 OSTIS

OSTIS is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

OSTIS is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with OSTIS.  If not, see <http://www.gnu.org/licenses/>.
-----------------------------------------------------------------------------
*/

#include "scp_keynodes.scsy"
#include "lib_keynodes.scsy"
#include "graph_keynodes.scsy"

//
// Добавляет посещение ребра @p edge и инцидентные ему вершины в маршрут @p route.
//
// @author Dmitry Lazurkin
//
procedure(add_edge_visit_to_route,
[[
	// Используемые ключевые узлы
	//
	1_;
	2_;
	"/graph_theory/keynode/arc_";

	// Используемые программы
	//
	add_vertex_visit_to_route;
	bin_ord_at_1;
	build_ord_bin_conn;
]],
[{
	arc2, arc4, conn,
	route_struct,
	route_visit,
	from_vertex_visit,
	to_vertex_visit,
	edge_visit,
	route, edge, from_vertex, to_vertex, edge_visit // переменные для параметров
}],
{[
	1_:  in_: route,       // связка отношения маршрут*
	2_:  in_: edge,        // ребро, посещение которого надо добавить в маршрут
	3_:  in_: from_vertex, // вершина, из которой выходит при посещении
	4_:  in_: to_vertex,   // вершина, в которую входим при посещении
	5_: out_: edge_visit   // посещение ребра (дуга в ориентированном графе структуры маршрута)
]})

	// Находим ориентированный граф структуры маршрута
	//
	callReturn([
		1_: fixed_: get_route_struct, 
		2_: {[
			1_: route, 
			2_: route_struct
		]}
	])

	// Находим отношение соответствия маршрута
	//
	callReturn([
		1_: fixed_: get_route_visit, 
		2_: {[
			1_: route, 
			2_: route_visit
		]}
	])

	// Добавим посещение вершины from_vertex в маршрут.
	//
	callReturn([
		1_: fixed_: add_vertex_visit_to_route, 
		2_: {[ 
			1_: route, 
			2_: from_vertex,
			3_: from_vertex_visit
		]}
	])

	// Создаем посещение связки
	//
	genEl([1_: assign_: node_: const_: edge_visit])

	// Добавим @p edge_visit как дугу в структуру маршрута
	//
	genElStr5([
		1_:  fixed_: route_struct,
		2_: assign_: arc_: pos_: const_: arc2,
		3_:  fixed_: edge_visit,
		4_: assign_: arc_: pos_: const_: arc4,
		5_:  fixed_: "/graph_theory/keynode/arc_"
	])

	// Укажем, что edge_visit является посещением edge
	//
	callReturn([
		1_: fixed_: build_ord_bin_conn, 
		2_: {[
			1_: route_visit, 
			2_: edge_visit, 
			3_: connective,
			4_: conn
		]}
	])

	// Укажем, что дуга edge_visit выходит из from_vertex_visit.
	//
	genElStr5([
		1_:  fixed_: edge_visit,
		2_: assign_: arc_: pos_: const_: arc2,
		3_:  fixed_: from_vertex_visit,
		4_: assign_: arc_: pos_: const_: arc4,
		5_:  fixed_: 1_
	])

	// Найдем посещение для вершины to_vertex.
	//
	callReturn([
		1_: fixed_: bin_ord_at_1, 
		2_: {[
			1_: route_visit, 
			2_: to_vertex, 
			3_: to_vertex_visit
		]}
	])

	// Укажем, что дуга @p edge_visit входит в @p to_vertex_visit.
	//
	genElStr5([
		1_:  fixed_: edge_visit,
		2_: assign_: arc_: pos_: const_: arc2,
		3_:  fixed_: to_vertex_visit,
		4_: assign_: arc_: pos_: const_: arc4,
		5_:  fixed_: 2_
	])

	return()
end
