/*
-----------------------------------------------------------------------------
This source file is part of OSTIS (Open Semantic Technology for Intelligent Systems)
For the latest info, see http://www.ostis.net

Copyright (c) 2011 OSTIS

OSTIS is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

OSTIS is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with OSTIS.  If not, see <http://www.gnu.org/licenses/>.
-----------------------------------------------------------------------------
*/
#include "scp_keynodes.scsy"
#include "graph_keynodes.scsy"
#include "lib_keynodes.scsy"

//
// Выводит на консоль неориентированный граф @p graph.
// @author Dmitry Lazurkin
//
procedure(print_graph,
[[
	edge_str =c= /" -- "/; // Так будет выглядеть ребро при выводе на консоль

	// Используемые ключевые узлы
	//
	"/graph_theory/keynode/edge_";
	"/graph_theory/keynode/vertex_";
	
	// Используемые программы
	//
	print_idtf;
	printnl_idtf;
]],
[{
	arc2, arc4, elt3,
	edges_set,
	vertexes_set, 
	printed_vertexes, 
	cur_edge, cur_vertex,
	v1,
	v2,
	g                        // переменные для параметров
}],
{[
	1_:  in_: g // неориентированный граф для вывода
]})
	// Формируем множество связок графа для итерации
	//
	searchSetStr5([
		1_:  fixed_: g,
		2_: assign_: arc2,
		3_: assign_: elt3,
		4_: assign_: arc4,
		5_:  fixed_: "/graph_theory/keynode/edge_",
		set3_: assign_: edges_set
	])

	// Создаем множество уже выведенных вершин
	//
	genEl([1_: assign_: node_: const_: printed_vertexes])

	// Вывод всех ребер графа.
	//
	label([next_edge])
		searchElStr3([
			1_:  fixed_: edges_set,
			2_: assign_: arc2,
			3_: assign_: cur_edge
		],, edge_iter_end)

		eraseEl([1_: fixed_: f_: arc2])
	
		// Получаем вершины, инцидентные ребру
		//
		callReturn([
			1_: fixed_: get_edge_vertexes, 
			2_: {[
				1_: cur_edge, 
				2_: v1, 
				3_: v2
			]}
		])
	
		// Выводим идентификатор первой вершины
		//
		callReturn([
			1_: fixed_: print_idtf, 
			2_: {[ 1_: v1 ]}
		])

		print([1_: fixed_: edge_str])

		// Выводим идентификатор второй вершины
		//
		callReturn([
			1_: fixed_: printnl_idtf, 
			2_: {[ 1_: v2 ]}
		])
	
		// Добавляем вершины в множество выведенных
		//
		genElStr3([
			1_:  fixed_: printed_vertexes,
			2_: assign_: arc_: pos_: const_: arc2,
			3_:  fixed_: v1
		])

		genElStr3([
			1_:  fixed_: printed_vertexes,
			2_: assign_: arc_: pos_: const_: arc2,
			3_:  fixed_: v2
		])
	
		// Пустая команда для перехода на следующую итерацию
		//
		nop(next_edge)

// Сюда попадем только, когда все ребра будут выведены на консоль
//
label([edge_iter_end])

	// Удалим уже пустое множество выводимых ребер
	//
	eraseEl([1_: f_: edges_set])

	// Вывод всех вершин, которые не имеют инцидентных дуг.
	// Формируем множество вершин графа для итерации
	//
	searchSetStr5([
		1_:  fixed_: g,
		2_: assign_: arc2,
		3_: assign_: elt3,
		4_: assign_: arc4,
		5_:  fixed_: "/graph_theory/keynode/vertex_",
		set3_: assign_: vertexes_set
	])

	// Цикл по вершинам
	//
	label([next_vertex])
		searchElStr3([
			1_:  fixed_: vertexes_set,
			2_: assign_: arc2,
			3_: assign_: cur_vertex
		],, return_label)

		eraseEl([1_: fixed_: f_: arc2])

		// Если вершина уже была выведена (т.е. она в множестве printed_vertexes)
		// то переходим на следующую итерацию.
		//
		searchElStr3([
			1_:  fixed_: printed_vertexes,
			2_: assign_: arc2,
			3_:  fixed_: cur_vertex
		], next_vertex)

		callReturn([
			1_: fixed_: printnl_idtf, 
			2_: {[ 1_: cur_vertex ]}
		])

		// Пустая команда для перехода на следующую итерацию
		//
		nop(next_vertex)

label([return_label])
	eraseEl([1_: f_: vertexes_set])
	return()
end
